plugins {
    id 'java'
    id 'com.gradleup.shadow' version '8.3.5'
}

group 'me.m0dii'
version 'j21-mc1.21.3-4.0.2'

archivesBaseName = 'M0-ExtraEnchants'

shadowJar {
    ['org.bstats', 'com.jeff_media'].each {
        relocate it, "me.m0dii.libs.$it"
    }

    minimize()

    archiveFileName = "M0-ExtraEnchants-${version}.jar"
}

repositories {
    mavenLocal()
    mavenCentral()

    flatDir {
        dirs 'libs'
    }

    ['https://jitpack.io',
     'https://repo.papermc.io/repository/maven-public/',
     'https://hub.spigotmc.org/nexus/content/repositories/snapshots/',
     'https://repo.jeff-media.com/public/',
     'https://repo.extendedclip.com/releases/'
    ].each { repo ->
        maven { url = uri(repo) }
    }
}

dependencies {
    compileOnly 'io.papermc.paper:paper-api:1.21.3-R0.1-SNAPSHOT'

    compileOnly 'me.clip:placeholderapi:2.11.6'
    compileOnly 'com.github.MilkBowl:VaultAPI:1.7'
    compileOnly name: 'Residence5.0.1.5'

    implementation "org.bstats:bstats-bukkit:2.2.1"
    implementation 'org.reflections:reflections:0.10.2'
    implementation group: 'com.jeff_media', name: 'MorePersistentDataTypes', version: '2.4.0'

    compileOnly 'org.projectlombok:lombok:1.18.30'
    annotationProcessor 'org.projectlombok:lombok:1.18.30'
}

compileJava.options.encoding = 'UTF-8'

java {
    toolchain.languageVersion.set(JavaLanguageVersion.of(21))
}

